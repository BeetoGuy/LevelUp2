buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven {
            name = "sonatype-nexus-public"
            url = "https://oss.sonatype.org/content/repositories/public/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    maven {
        name = 'sonatype-nexus-public'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
    maven { url "https://repo.spongepowered.org/maven"}
}

dependencies {
    compile files('libs/')

    compile "org.spongepowered:mixin:0.7.10-SNAPSHOT"
}

//Fix forges AT breaking everything because the AT checker is stupid
[tasks.deobfMcMCP, tasks.deobfMcSRG].each { it.failOnAtError = false }

jar{
    manifest {
        attributes(
                "FMLAT": 'lu_at.cfg',
                "Specification-Title": "levelup2",
                "Specification-Vendor": "Beeto",
                "Specification-Version": "1",
                "Implementation-Title": "levelup2",
                "Implementation-Vendor": "Beeto",
                "Implementation-Version": "${version}",
                "MixinConnector": "levelup2.mixin.Connector",
                'Premain-Class': 'org.spongepowered.tools.agent.MixinAgent',
                'Agent-Class': 'org.spongepowered.tools.agent.MixinAgent',
                "FMLCorePluginContainsFMLMod": true,
                'ForceLoadAsMod': true,
                'Can-Redefine-Classes': true,
                'Can-Retransform-Classes': true,
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                'TweakOrder': 0,
                "FMLCorePlugin": "levelup2.core.LevelUpCore",
                "MixinConfigs": "levelup2.mixins.json"
        )
    }
}

shadowJar {
    mainSpec.sourcePaths.clear()
    dependsOn reobfJar
    classifier 'shaded'
    exclude 'dummyThing'
    exclude 'MixinBootstrap-1.0.2.jar'
    dependencies {
        include dependency('org.spongepowered:mixin')
    }
    afterEvaluate {
        from zipTree(reobfJar.jar)
    }
}

artifacts {
    archives shadowJar
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier "dev"
}

artifacts {
    archives deobfJar
}

mixin {
    add sourceSets.main, "mixins.levelup2.refmap.json"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
